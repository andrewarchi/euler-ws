# Advent of Code 2020
# Day 3: Toboggan Trajectory
# https://adventofcode.com/2020/day/3

    "@bool"   import
    "@matrix" import
    "@string" import

    0 ^ readi retrieve # h
    0 ^ readi retrieve # w
    0 ^2 ^2 call matrix.read_chars jz .read_error

    0 ^2 ^2 1 3 call count_slope_trees
    ^ printi '\n' printc
    0 ^3 ^3 1 1 call count_slope_trees *
    0 ^3 ^3 1 5 call count_slope_trees *
    0 ^3 ^3 1 7 call count_slope_trees *
    0 ^3 ^3 2 1 call count_slope_trees *
    printi '\n' printc
    2drop end

# count_slope_trees returns the number of trees ('#') on the map along
# the given dy/dx slope.
# (addr h w dy dx -- count)
count_slope_trees:
    ^3 ^3 * 1- # len-1
    0 0        # count i
.count_slope_trees_loop:
    ^ ^6 / ^5 + ^6 * # y = (i/w + dy) * w
    swap ^4 + ^6 %   # x = (i + dx) % w
    +                # i = x + y
    ^2 ^1 j< .count_slope_trees_ret # i >= len
    # count += h[addr + i] == '#'
    swap ^7 ^2 + retrieve '#' = + swap
    jmp .count_slope_trees_loop
.count_slope_trees_ret:
    drop 6slide ret

.read_error:
    -1 "Error: invalid map format"
    call string.println_neg_stack end
