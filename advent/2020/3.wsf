# Advent of Code 2020
# Day 3: Toboggan Trajectory
# https://adventofcode.com/2020/day/3

    "@types/bool"   import
    "@types/matrix" import
    "@types/string" import

    0 ^ readi retrieve # h
    0 ^ readi retrieve # w
    0 ^2 ^2 call matrix.read_chars jz .read_error

    0 ^2 ^2 1 3 call count_slope_trees printi '\n' printc
    end

# (addr h w dy dx -- count)
count_slope_trees:
    ^3 ^3 * 1- # len-1
    0 0        # count i
.count_slope_trees_loop:
    ^ ^6 / ^5 + ^6 * # y = (i/w + dy) * w
    swap ^4 + ^6 %   # x = (i + dx) % w
    +                # i = x + y
    ^2 ^1 j< .count_slope_trees_ret # i >= len
    # count += h[addr + i] == '#'
    swap ^7 ^2 + retrieve '#' call bool.is_eq + swap
    jmp .count_slope_trees_loop
.count_slope_trees_ret:
    drop 6slide ret

.read_error:
    -1 "Error: invalid map format\n"
    call string.print_neg_stack end
