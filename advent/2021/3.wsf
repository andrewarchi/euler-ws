# Advent of Code 2021
# Day 3: Binary Diagnostic
# https://adventofcode.com/2021/day/3

    "@array"  import
    "@bool"   import
    "@int"    import
    "@math"   import
    "@string" import

    0 ^ readi retrieve # numbers
    0 ^ readi retrieve # bits
    0
.read_loop:
    ^ ^ 2 call int.read_base jz .read_error store
    1+
    ^ ^3 j< .read_loop
    drop
    0 ^2 call array.quicksort

    # Part 1
    0 0 # i epsilon
.part1_loop:
    0 ^4 ^3 call least_common_bit_value +
    swap 1+ swap
    ^1 ^3 j< .part1_loop # i < bits
    1slide
    2 ^2 ** 1- ^1 - # gamma = ((1<<bits)-1)&^epsilon
    * printi '\n' printc # gamma * epsilon
    2drop end

# least_common_bit_value returns the bit value (0 or 1) that is least
# common at the given bit position in the numbers at addresses [lo, hi).
# When 0 and 1 are equally-common, 0 is returned.
# (lo hi bit -- v)
least_common_bit_value:
    2 ^1 ** # 1 << bit
    ^3 0 # i count
.least_common_bit_value_loop:
    ^1 retrieve ^3 / 2% + # count += (nums[i]>>bit)&1
    swap 1+ swap # i++
    ^1 ^5 j< .least_common_bit_value_loop # i < hi
    1slide
    ^3 ^5 - 2/ < * # (count < (hi-lo)/2) << bit
    3slide ret

.read_error:
    -1 "Error: invalid binary format"
    call string.println_neg_stack 2drop end
