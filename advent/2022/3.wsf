# Advent of Code 2022
# Day 3: Rucksack Reorganization
# https://adventofcode.com/2022/day/3

    "@array"  import
    "@mem"    import
    "@string" import

    0 ^ readi retrieve # count
    0 # score
.read_loop:
    swap 3- ^ jn .read_break swap

    108 call string.read_line 2/

    0 108 ^2 call index
    54 108 ^2 + ^2 call index
    0 54 call intersect
    0 54 call array.sum +

    jmp .read_loop
.read_break:
    drop
    printi '\n' printc
    end

# priority converts an item in the alphabet a-zA-Z to its priority in
# 1-54.
# (item -- priority)
priority:
    # (1 - x/96)*26 + x%32
    1 ^1 96/ - 26* swap 32% +
    ret

# (table addr len -- )
index:
    ^2 54 call mem.clear
    ^1 + swap
.index_loop:
    ^ ^2 j= .index_break
    ^ retrieve call priority # priority
    ^ 1- ^4 +                # offset
    swap store               # table[offset] = priority
    1+
    jmp .index_loop
.index_break:
    2drop ret

# (table addr len -- score)
intersect:
    ^ ^2 + swap ^1 + swap
    0 # score
.intersect_loop:
    ^1 ^3 j= .intersect_break
    ^1 retrieve call priority # priority2
    ^ 1- ^4 +                 # offset
    ^ retrieve                # priority1 = table[offset]
    ^1 0 store                # table[offset] = 0
    * +                       # score += priority1
    swap 1+ swap
    jmp .intersect_loop
.intersect_break:
    2slide ret
